# Squire has two more types: Books (arrays) and Codices (dictionaries). Nothing
# _terribly_ exciting, they work generally how you'd expect.


# Books in Squire are created with the familiar `[..]` syntax. Unlike most other
# languages, Squire us `1`-index based (*cough*, sorry, `I`-index based.)
languages = [𝔖𝔮𝔲𝔦𝔯𝔢, ℜ𝔲𝔰𝔱, ℭ]
proclaim("I like {languages}") #=> I like [Squire, Rust, C]

# Fetching a page which doesn't exist in the book will yield `ni`.
proclaim(languages[V]) #=>ni

# However, assigning to a page that's too large will fill the rest of the book
# with `ni`:
languages[V] = 𝔎𝔫𝔦𝔤𝔥𝔱
proclaim(languages) #=> [Squire, Rust, C, ni, Knight]

# You can also index using negative numbers to start from the end
proclaim(languages[-I]) #=> Knight

# The amount of pages in a book can be retrieved via `length`:
proclaim("There are {languages.length} languages.") #=> There are V languages.

# Lastly, you can insert or delete elements via `insert` and `delete`.
# Note that these will eventually become member functions on books:
insert(languages, I, "Quest")
proclaim(languages) #=> [Quest, Squire, Rust, C, ni, Knight]
delete(languages, V)
proclaim(languages) #=> [Quest, Squire, Rust, C, Knight]


# Codices are Squire's dictionaries/hashmaps/hashes/maps/whatever you want to
# call them. 
prices = { 𝔪𝔢𝔞𝔡: III, 𝔞𝔩𝔢: IV, 𝔡𝔦𝔫𝔫𝔢𝔯: X }
proclaim("The prices at my tavern are: {prices}.")
#=> The prices at my tavern are: {mead: III, ale: IV, dinner: X}.

# Uh oh, our shipment of mead is late! Better jack up the prices
prices[𝔪𝔢𝔞𝔡] = VII
proclaim("Mead now costs {prices[𝔪𝔢𝔞𝔡]} coins") #=> Mead now cost VII coins.

# Like books, accessing nonexistent elements returns `ni`:
proclaim("Mutton costs {prices[𝔪𝔲𝔱𝔱𝔬𝔫]}.") #=> Mutton costs ni.

# Like books, the amount of entries in a codex can be retrieved via `length`:
proclaim("There are {prices.length} items.") #=> There are III items.

# Alas, we've run out of mead. Let's take it off the menu.
delete(prices, 𝔪𝔢𝔞𝔡)
proclaim("The prices at my tavern are now: {prices}.")
#=> The prices at my tavern are now: {dinner: X, ale: IV}.
